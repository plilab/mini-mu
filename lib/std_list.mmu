import "std_nat" (eq)

fn list_append as bs k :=
  match as with 
    Nil -> bs . k
  | List:: a as' -> 
    letcc k' = { rest -> List:: a rest . k } in 
    list_append @ as' bs k';

fn list_map f xs k :=
  xs . { Nil -> Nil . k
       | List:: x xs' ->
            do y <- f(x),
               ys' <- list_map(f, xs')
            then List:: y ys' . k };

fn list_reverse xs acc k :=
    xs . { Nil -> acc . k
         | List:: x xs' -> 
            xs' (List:: x acc) k @ list_reverse };

fn list_length xs k :=
  xs . { Nil -> Z . k
       | List:: x xs' -> xs' { rest -> S rest . k } @ list_length };

fn list_contains xs y k :=
   xs . { Nil -> False . k
        | List:: x xs' -> 
            eq @ x y { True -> True . k 
                     | _ -> list_contains @ xs' y k } };

export list_append, list_map, list_length, list_reverse, list_contains
