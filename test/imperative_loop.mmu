list_map = mu[ Ap3 f xs k -> 
                < xs |> mu[ Nil -> < Nil |> k > 
                          | List:: x xs' -> 
                            < f |> Ap x mu[ y -> 
                                < list_map |> Ap3 f xs' mu[ ys' -> < List:: y ys' |> k > ] > ] > ] > ];

inc = mu[ Ap x k -> < S x |> k > ];

lt = mu[ Ap3 a b k -> < a |> mu[ Z -> 
                                      < b |> mu[ Z -> < False |> k > 
                                               | S b' -> < True |> k > ] >
                                   | S a' -> 
                                      < b |> mu[ Z -> < False |> k >
                                               | S b' -> < lt |> Ap3 a' b' k > ] > ] > ];

for_loop = mu[ Ap5 i end xs body k ->
  < lt |> Ap3 i end mu[ is_less ->
    < is_less |> mu[ True ->
                      < body |> Ap3 i xs mu[ xs' ->
                          < S i |> mu[ i_next -> < for_loop |> Ap5 i_next end xs' body k > ] > ] >
                   | False -> < xs |> k > ] > ] > ];

map_once = mu[ Ap3 i xs k -> < list_map |> Ap3 inc xs k > ];

main = mu[ Halt ->
    < for_loop |> Ap5 Z (S (S (S Z))) (List:: (S (S Z)) (List:: (S Z) (List:: Z Nil))) map_once Halt > ]